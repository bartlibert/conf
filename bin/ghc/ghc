#!/usr/bin/perl
use warnings;
use strict;
use FindBin;
use Cwd 'realpath';
use File::Basename;
use File::Spec;

=pod

ghc does something dumb with timers for their concurrency stuff that causes a
huge amount of wakeups, disable that

=cut

sub any (&@) {
    my $code = shift;
    !!grep { $code->() } @_;
}

sub firstval (&@) {
    my $code = shift;
    for (@_) {
        return $_ if $code->();
    }
    return;
}

my $bin = realpath $FindBin::Bin;
my $executable = fileparse($0);

if (any { /xmonad/ } @ARGV) {
    unshift @ARGV, '-with-rtsopts=-V0';
}

my $exepath = (
    firstval { -f $_ }
    map { File::Spec->catfile($_, $executable) }
    grep { defined $_ && $_ ne $bin }
    map { realpath $_ }
    split( /:/, $ENV{PATH} )
);

exec($exepath, @ARGV);
